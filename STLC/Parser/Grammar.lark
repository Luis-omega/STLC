SPACES : / |\n/+
%ignore SPACES

// Python re doesn't match "\n" by defect when using `.`
COMMENT_REST_OF_LINE: /#[^\n]*/

%ignore COMMENT_REST_OF_LINE


TRUE.2 : "True"
FALSE.2 : "False"

INT_LITERAL.2 : /-?[1-9][0-9_]*/  | /0[0_]*/

BOOL : "Bool"
INT_TYPE : "Int" 
UNIT_TYPE : "Unit"

IF.2 : "if"
THEN.2 : "then"
ELSE.2 : "else"

LAMBDA : "\\"

VARIABLE_CHARACTER: /[a-zA-Z_]/
VARIABLE : VARIABLE_CHARACTER+

ARROW : "->"
COLON : ":"
SEMICOLON  : ";"
EQUAL : "="
LPAREN : "("
RPAREN : ")"

PLUS : "+"
MINUS : "-"
STAR : "*"
DIV :"/"
LE: "<"
GE: ">"
LEQ.2 : "<=" 
GEQ.2 : ">="
CMP.2 : "=="
NEQ.2 : "/="
AND : "&"
OR : "|"
NOT : "~"


variable : VARIABLE


expression_atom : variable -> expression_atom_variable
  | INT_LITERAL  -> expression_atom_int
  | TRUE -> expression_atom_true
  | FALSE -> expression_atom_false 
  | LAMBDA variable ARROW expression -> expression_atom_lambda
  | IF expression THEN expression ELSE expression -> expression_atom_if
  | LPAREN expression RPAREN -> expression_atom_parens
  | LPAREN expression COLON type RPAREN -> expression_atom_annotation

expression_application : expression_atom -> expression_application_single
  | expression_atom expression_atom+

expression_operator{operators,next_expression}: next_expression (operators next_expression)+  ->  expression_operator
  | next_expression -> expression_operator_single

_multiplicative_operators: DIV | STAR

?expression_multiplicative: expression_operator{_multiplicative_operators,expression_application}

_additive_operators: PLUS | MINUS

?expression_additive: expression_operator{_additive_operators, expression_multiplicative}

// This one has to be done by hand to forbid multiple uses of 
// comparation operators at parsing time
_order_operators : LEQ | GEQ | LE | GE
?expression_order : expression_additive _order_operators expression_additive
  | expression_additive


_comparative_operators: CMP | NEQ
?expression_comparative : expression_operator{_comparative_operators, expression_order}

?expression_not :  expression_operator{NOT,expression_comparative}

?expression_and : expression_operator{AND, expression_not}

?expression_or : expression_operator{OR,expression_and}

?expression : expression_or

type_arrow : type ARROW type

type_parens: LPAREN type RPAREN

type_bool: BOOL

type_int: INT_TYPE

type_unit: UNIT_TYPE

?type : type_bool | type_int | type_unit | type_parens | type_arrow

variable_declaration : variable COLON type SEMICOLON

variables : variable+

variable_definition : variable EQUAL expression SEMICOLON -> variable_definition_alone
  | variable variables EQUAL expression SEMICOLON

top : (variable_declaration | variable_definition)*
